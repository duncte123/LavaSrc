plugins {
    id "dev.arbjerg.lavalink.gradle-plugin" version "1.0.15"
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

archivesBaseName = "lavasrc-plugin"
lavalinkPlugin {
    name = "lavasrc-plugin"
    apiVersion = gitHash("436763fe09b32fe26ee40cd238e3d1e910749cf9")
    serverVersion = gitHash("436763fe09b32fe26ee40cd238e3d1e910749cf9")
    configurePublishing = false
}


dependencies {
    implementation project(":main")
    compileOnly "com.github.topi314.lavasearch:lavasearch:1.0.0"
    implementation "com.github.topi314.lavasearch:lavasearch-plugin-api:1.0.0"
    compileOnly "com.github.topi314.lavalyrics:lavalyrics:01bf4e7"
    implementation "com.github.topi314.lavalyrics:lavalyrics-plugin-api:01bf4e7"

    // Copy lyrics.kt from main
    project(":main").configurations.implementation.dependencies.forEach {
        if (it.group == "dev.schlaubi.lyrics") {
            add("implementation", it)
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId archivesBaseName
        }
    }
}
